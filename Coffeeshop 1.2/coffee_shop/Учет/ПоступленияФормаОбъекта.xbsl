импорт Справочники

метод Провести(Команда: ОбычнаяКоманда)
    ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведен
    Записать()
    Закрыть()

    знч УведомлениеОПроведении = новый Уведомление(
        "Проведение документа",
        "Документ №%{Объект.Номер} проведен")

    знч КомандаОткрытия = новый КомандаСПараметром<Уведомление>(
        (КомандаСПараметром, Уведомление) -> Поступления.СоздатьФормуОбъекта(Уведомление.Данные).Открыть(),
        "Открыть документ")

    УведомлениеОПроведении.Команды = новый ФрагментКомандногоИнтерфейса<КомандаСПараметром<Уведомление>>([
            КомандаОткрытия])
    УведомлениеОПроведении.Данные = КлючОбъекта
    УведомлениеОПроведении.ОценкаИнформации = ОценкаИнформации.Положительная
    УведомлениеОПроведении.СохранитьВЦентреУведомлений = Ложь
    УведомлениеОПроведении.Показать()
;

метод Сохранить(Команда: ОбычнаяКоманда)
    ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Запись
    Записать()
;

метод ОтменитьПроведение(Команда: ОбычнаяКоманда)
    если не Объект.Проведен
        выбросить новый ИсключениеНедопустимоеСостояние(
                "Документ не проведен.")
    ;
    знч Ответ = Диалог.Вопрос("Отменить проведение?",
        [СтандартнаяКнопкаДиалога.Да, СтандартнаяКнопкаДиалога.Нет],
        "%Заголовок будет отменен и станет черновиком.")

    если Ответ == СтандартнаяКнопкаДиалога.Да
        ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения
        Записать()
    ;
;

@Обработчик
метод ПослеСоздания()
    пер ТекущийСотрудник = УказатьСотрудника()
    если ТекущийСотрудник != Неопределено
        если ТекущийСотрудник.Должность.Представление() == "Бариста" или ТекущийСотрудник.Должность.Представление()
                == "Управляющий"
            если ТекущийСотрудник.Владелец != Объект.Кофейня и не ЭтоНовый()
                Закрыть()
                выбросить новый ИсключениеНедопустимоеСостояние(
                    "Нельзя просматривать документы кофеен, в которых вы не работаете.")
            иначе если ЭтоНовый()
                Компоненты.Кофейня.Значение = ТекущийСотрудник.Владелец
            ;
            Компоненты.Кофейня.Доступность = Ложь
            Компоненты.Кофейня.ОткрытьЗначение.Доступность = Ложь
            Компоненты.Контрагент.ОткрытьЗначение.Доступность = Ложь
        ;
    ;
;

@НаСервере @ДоступноСКлиента
статический метод УказатьСотрудника(): Сотрудники.Объект?
    если Пользователи.ТекущийПользователь.ЗагрузитьОбъект().Администратор
        возврат Неопределено
    иначе
        пер КодСотрудника = Пользователи.ТекущийПользователь.ЗагрузитьОбъект().Утверждения["Код"]
        возврат Сотрудники.НайтиПоКоду(КодСотрудника).ЗагрузитьОбъект()
    ;
;

метод КоличествоПриИзменении(Источник: ПолеВвода<Объект?>, Событие: СобытиеПриИзменении<Объект?>,
        ДанныеСтроки: Поступления.Товары)

    ДанныеСтроки.Сумма = ДанныеСтроки.Цена * ДанныеСтроки.Количество
;

метод ЦенаПриИзменении(Источник: ПолеВвода<Объект?>, Событие: СобытиеПриИзменении<Объект?>,
        ДанныеСтроки: Поступления.Товары)

    ДанныеСтроки.Сумма = ДанныеСтроки.Цена * ДанныеСтроки.Количество
;

метод АвтоЦенаТовара(Источник: ПолеВвода<Объект?>, Событие: СобытиеПриИзменении<Объект?>,
        ДанныеСтроки: Поступления.Товары)

    пер ЦенаТовара = ПолучитьЦенуТовара(ДанныеСтроки.Номенклатура)
    ДанныеСтроки.Цена = ЦенаТовара
    ДанныеСтроки.Сумма = ЦенаТовара * ДанныеСтроки.Количество
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьЦенуТовара(Номенклатура: Номенклатура.Ссылка): Число?
    пер ЦенаТовара = Номенклатура.ЗагрузитьОбъект()
    возврат ЦенаТовара.ЗакупочнаяЦена
;