импорт Основное

@Обработчик
метод ПередЗаписью(До: Поступления.Данные, ПараметрыЗаписи: Поступления.ПараметрыЗаписи)
    если не ЭтоНовый() и ПометкаУдаления и До.Проведен
        выбросить новый ИсключениеНедопустимоеСостояние(
                "Нельзя удалить проведенный документ. Сначала отмените проведение.")
    ;
     
    выбор ПараметрыЗаписи.РежимЗаписи
    когда Проведен
        для Строка из Товары
            пер Товар = Строка.Номенклатура.ЗагрузитьОбъект()
            если Товар.СобственноеПроизводство
                выбросить новый ИсключениеВыполнения("Товар '" + Товар.Наименование
                    + "' не может быть записан в документ поступления товаров.")
            ;
        ;
        если ПометкаУдаления
            выбросить новый ИсключениеНедопустимоеСостояние(
                "Помеченный на удаление документ не может быть проведен.")
        ;
        Проведен = Истина
            
    когда ОтменаПроведения
        Проведен = Ложь
        
    когда Неопределено
        если ПометкаУдаления
            Проведен = Ложь
        ;
    ;
    СуммаПоДокументу=Товары.Преобразовать(Товары -> Товары.Сумма).Сумма()
;

@Обработчик
метод ПослеЗаписи(До: Поступления.Данные, ПараметрыЗаписи: Поступления.ПараметрыЗаписи)
    если ПараметрыЗаписи.РежимЗаписи == РежимЗаписиДокумента.Проведен или (ПараметрыЗаписи.РежимЗаписи
            == РежимЗаписиДокумента.Запись и Проведен)
        ВыполнитьПроведение()

    иначе если ПараметрыЗаписи.РежимЗаписи == РежимЗаписиДокумента.ОтменаПроведения или ПометкаУдаления != До
            .ПометкаУдаления и ПометкаУдаления
        ОтменитьПроведение()
    ;
;

метод ВыполнитьПроведение()
    знч НаборЗаписей = новый ОстаткиНоменклатуры.НаборЗаписей()
    НаборЗаписей.Фильтр.Установить(Регистратор = Ссылка)
    
    для Строка из Товары
        пер Товар = Строка.Номенклатура.ЗагрузитьОбъект()
        пер Множитель = 1
        если Товар.УчитыватьПоКоличеству
            Множитель = МножительЦен.множитель
        иначе если Товар.УчитыватьПоКоличеству == Ложь и Товар.Продаваемый
            Множитель = МножительЦен.множитель
        ;
        НаборЗаписей.ДобавитьЗапись(
            Период = Дата,
            ВидЗаписи = ВидЗаписиРегистраНакопления.Приход,
            Номенклатура = Строка.Номенклатура,
            Кофейня = Кофейня,
            Количество = Строка.Количество,
            Сумма = Строка.Сумма * Множитель)
    ;

    НаборЗаписей.Записать()
;

метод ОтменитьПроведение()
    знч НаборЗаписей = новый ОстаткиНоменклатуры.НаборЗаписей()
    НаборЗаписей.Фильтр.Установить(Регистратор = Ссылка)
    НаборЗаписей.Записать()
;