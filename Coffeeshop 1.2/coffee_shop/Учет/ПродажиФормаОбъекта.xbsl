импорт Справочники
импорт Основное

метод Провести(Команда: ОбычнаяКоманда)
    ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведен
    Записать()
    Закрыть()

    знч УведомлениеОПроведении = новый Уведомление(
        "Проведение документа",
        "Документ №%{Объект.Номер} проведен")

    знч КомандаОткрытия = новый КомандаСПараметром<Уведомление>(
        (КомандаСПараметром, Уведомление) -> Продажи.СоздатьФормуОбъекта(Уведомление.Данные).Открыть(),
        "Открыть документ")

    УведомлениеОПроведении.Команды = новый ФрагментКомандногоИнтерфейса<КомандаСПараметром<Уведомление>>([
            КомандаОткрытия])
    УведомлениеОПроведении.Данные = КлючОбъекта
    УведомлениеОПроведении.ОценкаИнформации = ОценкаИнформации.Положительная
    УведомлениеОПроведении.СохранитьВЦентреУведомлений = Ложь
    УведомлениеОПроведении.Показать()
;

метод Сохранить(Команда: ОбычнаяКоманда)
    ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Запись
    Записать()
;

метод ОтменитьПроведение(Команда: ОбычнаяКоманда)
    если не Объект.Проведен
        выбросить новый ИсключениеНедопустимоеСостояние(
                "Документ не проведен.")
    ;
    знч Ответ = Диалог.Вопрос("Отменить проведение?",
        [СтандартнаяКнопкаДиалога.Да, СтандартнаяКнопкаДиалога.Нет],
        "%Заголовок будет отменен и станет черновиком.")

    если Ответ == СтандартнаяКнопкаДиалога.Да
        ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения
        Записать()
    ;
;

метод КоличествоПриИзменении(Источник: ПолеВвода<Объект?>, Событие: СобытиеПриИзменении<Объект?>,
        ДанныеСтроки: Продажи.СписокТоваров)
        
    ДанныеСтроки.Сумма = ДанныеСтроки.Цена * ДанныеСтроки.Количество
;

метод ЦенаПриИзменении(Источник: ПолеВвода<Объект?>, Событие: СобытиеПриИзменении<Объект?>,
        ДанныеСтроки: Продажи.СписокТоваров)
        
    ДанныеСтроки.Сумма = ДанныеСтроки.Цена * ДанныеСтроки.Количество
;

метод АвтоЦенаТовара(Источник: ПолеВвода<Объект?>, Событие: СобытиеПриИзменении<Объект?>,
        ДанныеСтроки: Продажи.СписокТоваров)
        пер ВыбраннаяНоменклатура = ПолучитьТовар(ДанныеСтроки.Товар)
        если ВыбраннаяНоменклатура
            пер ПолученнаяЦена = ПолучитьЦенуТовара(ДанныеСтроки.Товар)
            ДанныеСтроки.Цена = ПолученнаяЦена * МножительЦен.множитель
            ДанныеСтроки.Сумма = ДанныеСтроки.Цена * ДанныеСтроки.Количество
        иначе
            пер ЦенаТовара = ПолучитьЦенуНеКолич(ДанныеСтроки.Товар)
            ДанныеСтроки.Цена = ЦенаТовара
            ДанныеСтроки.Сумма = ЦенаТовара * ДанныеСтроки.Количество
        ;
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьЦенуТовара(Номенклатура: Номенклатура.Ссылка): Число?
    пер ЦенаТовара = Номенклатура.ЗагрузитьОбъект()
    возврат ЦенаТовара.ЗакупочнаяЦена
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьТовар(Номенклатура: Номенклатура.Ссылка): Булево?
    пер Товар = Номенклатура.ЗагрузитьОбъект()
    возврат Товар.УчитыватьПоКоличеству
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьЦенуНеКолич(Номенклатура: Номенклатура.Ссылка): Число?
    пер Товар = Номенклатура.ЗагрузитьОбъект()
    возврат Товар.Цена
;

@Обработчик
метод ПослеСоздания()
    пер ТекущийСотрудник = УказатьСотрудника()
    если ТекущийСотрудник != Неопределено
        если ЭтоНовый()
            если ТекущийСотрудник.Должность.Представление() == "Управляющий"
                выбросить новый ИсключениеНедопустимоеСостояние(
                    "Управляющие не могут создавать новые заказы.")
            ;
            Компоненты.Сотрудник.Значение = ТекущийСотрудник.Ссылка
        иначе если ТекущийСотрудник.Владелец != Объект.Кофейня и не ЭтоНовый()
            Закрыть()
            выбросить новый ИсключениеНедопустимоеСостояние(
                "Нельзя просматривать документы кофеен, в которых вы не работаете.")
        ;
        Компоненты.Сотрудник.Доступность = Ложь
        Компоненты.Сотрудник.ОткрытьЗначение.Доступность = Ложь
        Компоненты.Кофейня.Значение = ТекущийСотрудник.Владелец
        Компоненты.Кофейня.Доступность = Ложь
        Компоненты.Кофейня.ОткрытьЗначение.Доступность = Ложь
    ;
;

метод СотрудникПриИзменении(Источник: ПолеВвода<cs::coffee_shop::Справочники::Сотрудники.Ссылка?>, Событие: СобытиеПриИзменении<cs::coffee_shop::Справочники::Сотрудники.Ссылка?>)
    Компоненты.Кофейня.Значение = ПолучитьКофейнюСотрудника(Компоненты.Сотрудник.Значение)
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьКофейнюСотрудника(Сотрудник: Сотрудники.Ссылка): Кофейни.Ссылка
    пер ЗагруженныйСотрудник = Сотрудник.ЗагрузитьОбъект()
    возврат ЗагруженныйСотрудник.Владелец
;

@НаСервере @ДоступноСКлиента 
статический метод УказатьСотрудника(): Сотрудники.Объект?
    если Пользователи.ТекущийПользователь.ЗагрузитьОбъект().Администратор
        возврат Неопределено
    иначе
        пер КодСотрудника = Пользователи.ТекущийПользователь.ЗагрузитьОбъект().Утверждения["Код"]
        возврат Сотрудники.НайтиПоКоду(КодСотрудника).ЗагрузитьОбъект()           
    ;
;