импорт Справочники

метод Провести(Команда: ОбычнаяКоманда)
    если Объект.Проведен
        выбросить новый ИсключениеВыполнения("Этот документ уже проведен.")
    ;
    
    ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведен
    Записать()
    Закрыть()
    знч УведомлениеОПроведении = новый Уведомление(
        "Проведение документа",
        "Документ №%{Объект.Номер} проведен.")
        
    знч КомандаОткрытия = новый КомандаСПараметром<Уведомление>(
        (КомандаСПараметром, Уведомление) -> ПриемНаРаботу.СоздатьФормуОбъекта(Уведомление.Данные).Открыть(),
        "Открыть документ")
        
    знч Сотрудник = ПоискСотрудника(Объект.Ссылка)
    
    знч УведомлениеОНовомСотруднике = новый Уведомление(
        "Новый сотрудник добавлен",
        "Нужно заполнить данные о новом сотруднике №%{Сотрудник.Код}")
 
    знч КомандаОткрытияНовогоСотрудника = новый КомандаСПараметром<Уведомление>(
        (КомандаСПараметром, Уведомление) -> Сотрудники.СоздатьФормуОбъекта(Сотрудник.Ссылка).Открыть(),
        "Открыть страницу нового сотрудника")

    УведомлениеОПроведении.Команды = новый ФрагментКомандногоИнтерфейса<КомандаСПараметром<Уведомление>>([
            КомандаОткрытия])
    УведомлениеОПроведении.Данные = КлючОбъекта
    УведомлениеОПроведении.ОценкаИнформации = ОценкаИнформации.Положительная
    УведомлениеОПроведении.СохранитьВЦентреУведомлений = Ложь
    УведомлениеОПроведении.Показать()
    
    если Объект.Дата == Дата.Сейчас()
        УведомлениеОНовомСотруднике.Команды = новый ФрагментКомандногоИнтерфейса<КомандаСПараметром<Уведомление>>([
                КомандаОткрытияНовогоСотрудника])
        УведомлениеОНовомСотруднике.Данные = Сотрудник.Код
        УведомлениеОНовомСотруднике.ОценкаИнформации = ОценкаИнформации.Положительная
        УведомлениеОНовомСотруднике.СохранитьВЦентреУведомлений = Истина
        УведомлениеОНовомСотруднике.Показать()
    ;
;

метод Сохранить(Команда: ОбычнаяКоманда)
    ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Запись
    Записать()
;

@НаСервере @ДоступноСКлиента
статический метод ПоискСотрудника(Документ: ПриемНаРаботу.Ссылка): Сотрудники.Объект?
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Ссылка КАК СотрудникСсылка
        ИЗ
            Сотрудники
        ГДЕ
            ДокументОПриеме == %Документ
    }
    пер ЗагруженныйСотрудник: Сотрудники.Объект? = Неопределено
    для Строка из Запрос.Выполнить()
        ЗагруженныйСотрудник = Строка.СотрудникСсылка.ЗагрузитьОбъект()
    ;
    возврат ЗагруженныйСотрудник
;

@Обработчик
метод ПослеСоздания()
    если ЭтоНовый()
        Компоненты.Логин.Видимость = Ложь
    ;
;